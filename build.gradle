plugins {
    id 'org.springframework.boot' version '2.3.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'io.github.wyparks2'
    version '0.0.0'
    sourceCompatibility = JavaVersion.VERSION_11

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'

        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    test {
        useJUnitPlatform()
    }
}

project(':common') {
    jar.enabled = true
    bootJar.enabled = false

    dependencies {
        compile 'org.springframework.kafka:spring-kafka'
        compile 'com.fasterxml.jackson.core:jackson-databind:2.11.2'
        compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.11.2'
        compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.11.2'
    }
}

project(':kafka-producer') {
    jar.enabled = false
    bootJar.enabled = true

    dependencies {
        implementation project(':common')

        implementation 'org.springframework.boot:spring-boot-starter-web'
    }
}

project(':kafka-consumer') {
    jar.enabled = false
    bootJar.enabled = true

    dependencies {
        implementation project(':common')
    }
}
